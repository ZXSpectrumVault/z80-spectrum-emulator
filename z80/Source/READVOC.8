;
;    This file is part of the registered Spectrum emulator package 'Z80'
;    version 3.04, and may not be distributed.  You may use this source for
;    other PC based Spectrum or Z80 emulators only after permission.  It is
;    however permitted to use this source file or parts thereof for Spectrum
;    or Z80 emulators on non-PC based machines, provided that the source is
;    acknowledged.
;
;                                                     Gerton Lunter, 10/8/96
;

;
;READVOC - Utility to record a sample from the tape LPT interface, and
;          store it in a VOC voice file.
;
;          Gerton Lunter, 19/9/93
;
;          3/5/94: Modified to read from SoundBlaster too
;

        jmp start

vocheader:
        db "Creative Voice File"
        db 01a,01a,0,0a,1,029,011
datablock:
        db 1,0,0,0,0,0
terminator:
        db 0

drempel equ 10

vochandle       dw 0
buffer          dw ?                    ;segment address of buffer
buftop          dw ?                    ;top of buffer (segment)
tunit           dw ?                    ;sample unit (in 1192755 Hz ticks)
timinter:       dd ?                    ;address of timer interrupt
kbdinter:       dd ?                    ;address of keyboard interrupt
lptadr          dw ?                    ;base address of LPT port
baseadr         dw 0
samplefreq      dw 20                   ;sample frequency in kHz
signaltest      db 0ff                  ;flag for signal test

keypress        db 0                    ;0ff if key pressed
escpress        db 0                    ;0ff if ESC pressed
keydown         db 0ff                  ;bit 7=0 if key down
break           db 0                    ;080 if next scancode is a break code

original21      db ?

memerr: mov dx,memerror
error:  mov ah,9
        int 021
        mov ax,04c01            ;Terminate
        int 021

start:  mov sp,offset xlatbuf
        mov di,offset xlatbuf
        mov cx,256
        xor al,al
initxlatbuf:
        stosb
        inc al
        dec cx
        test cl,0f
        if e sub al,0f
        test cl,cl
        jne initxlatbuf
        mov dx,intromsg
        mov ah,9
        int 021
        mov ah,04a
        mov bx,comsize_para
        int 021                 ;shrink memory block for program
        jc memerr               ;Whoops!  Wonder what the stack has overwritten...
        mov ah,048
        mov bx,0ffff
        int 021                 ;find memory size
        cmp ax,8
        jne memerr
        cmp bx,01000
        jc memerr
        mov ah,048
        int 021                 ;allocate memory
        jc memerr
        mov buffer,ax
        add ax,bx
        mov buftop,ax
        xor ax,ax
        mov es,ax
        mov ax,es:[0408]
        inc ax
        mov lptadr,ax
        mov ax,es:[8*4]
        mov [timinter],ax
        mov ax,es:[8*4+2]
        mov [timinter+2],ax
        mov ax,es:[9*4]
        mov [kbdinter],ax
        mov ax,es:[9*4+2]
        mov [kbdinter+2],ax
        mov si,081
scanline:
        lodsb
        cmp al,0d
        je usage
        cmp al,020
        je scanline
        cmp al,'-'
        je option
        cmp al,'/'
        je option
        mov dx,si
        dec dx
scanname:
        lodsb
        cmp al,020
        je filenameend
        cmp al,0d
        jne scanname
filenameend:
        mov b[si-1],0
        jmp open_outputfile

option: lodsb
        and al,0df
        cmp al,'B'
        je setlpt
        cmp al,'R'
        je setfreq
        cmp al,'T'
        je sigtest
        cmp al,'S'
        je setbase
usage:  mov dx,helpmsg
        mov ah,9
        int 021
        mov ax,04c00
        int 021

setlpt: call getnum
        sub ax,1
        jc usage
        cmp ax,4
        jae usage
        add ax,ax
        mov bx,ax
        mov ax,es:[bx+0408]
        inc ax
        mov lptadr,ax
        jmp scanline

setfreq:
        call getnum
        cmp ax,5
        jb usage
        cmp ax,140
        ja usage
        mov samplefreq,ax
        jmp scanline

sigtest:
        mov signaltest,0
        jmp scanline

setbase:
        mov baseadr,0220
        lodsb
        cmp al,020
        je setbase
        cmp al,9
        je setbase
        dec si
        cmp al,'0'
        if b jmp scanline
        cmp al,'9'
        if a jmp scanline
        call getnum         ;decimal
        mov dx,baseerr
        sub ax,200
        if c jmp error
        cmp ax,80
        if a jmp error
        mov bl,10
        div bl
        test ah
        if ne jmp error
        mov cl,4
        shl al,cl
        mov ah,02
        mov baseadr,ax
        jmp scanline


open_outputfile:                ;not yet...
        push dx                 ;save start of file name for later
        mov ax,1193             ;1193180 Hz (of timer clock) / 1000
        xor dx,dx
        div samplefreq          ;get value of timer register
        add dx,dx               ; (11.9 <= AL <= 238.6 )
        cmp dx,samplefreq
        sbb ax,-1               ;round to nearest value
        mov tunit,ax
        mov ah,9
        mov dx,startmsg
        int 021
        mov ax,buftop
        sub ax,buffer           ;# of paragraphs
        call printlength        ;On average, about 256 samples/16 bytes (16*15)
        mov ah,9                ; (Especially true with high sample rates)
        mov dx,startmsg2        ; Best case: 30 samples a byte
        int 021                 ; Worst case: 2 samples a byte
        call install
        mov dx,lptadr
        mov di,outputbuf
        mov bx,outputbuf
        mov cx,8                ;Repeat the test eight times, to make sure
reptest:                        ; that if the test passes it's not because
        xor ax,ax               ; we were lucky because of synchronisation
        mov ds,ax               ; between timer and executed code.
        cli
        mov w[4*8],my_timinter2
        in al,021
        and al,0fe
        out 021,al              ;Enable timer interrupt
        xor al,al
        cld
        sti                     ;Enable interrupts,
waitinter0:                     ;and wait for the first one
        test al,al
        jz waitinter0
        in al,dx                ;Do the other instructions done by normal
        stosb                   ; timer interrupt. Also reset AL to <>020
        mov ah,es:[bx+1]        ;Slow instruction
        cmp es:keypress,0       ;Another one
        add bx,1                ;And another one
        mov ah,2                ;And another
        mov ah,al               ;Save flag
waitinter:
        cmp al,020
        jnz waitinter           ;Wait for an interrupt
        cli
        in al,021
        or al,1                 ;Don't generate timer interrupts
        out 021,al
        mov w[4*8],my_timinter  ;Reset vector
        push cs
        pop ds                  ;Reset DS
        sti
        cmp ah,020              ;If AH=020 then there has been another interrupt
        if z jmp too_slow       ; between the IN AL,DX and waitinter
        loop reptest            ;Loop 8 times
        cmp baseadr,0
        if e jmp skip_sbtest
		mov	al,1
        mov dx,baseadr
        add dx,6                ;DSP_RESET
        out dx,al               ;reset
        in al,dx
        in al,dx
        in al,dx
        in al,dx
        xor al,al
		out	dx,al
        mov cx,100
        mov dx,baseadr
        add dx,0e               ;DSP_DATA_AVAIL
datawait:
        in al,dx
        add al,al
        jc gotdata              ;a byte is waiting...
        loop datawait
        jmp noblaster
gotdata:
        mov dx,baseadr
        add dx,0a               ;DSP_READ_DATA
        in al,dx
        cmp al,0AAh             ;Is it the ID byte?
        je YepSB                ;Found the ID byte
        loop datawait
noblaster:
        mov dx,blasterr
error1: call reset
        jmp error
YepSB:
        mov dx,baseadr
        add dx,0c               ;dsp_write_status
        in al,dx
        add al,al
        jc yepsb                ;wait until okay to write
        mov al,020              ;get sample
        out dx,al               ;dsp_write_data
        xor ax,ax
        mov ds,ax
        cli
        mov w[4*8],my_timinter4
        push cs
        pop ds
        in al,021
        and al,0fe
        out 021,al              ;Enable timer interrupt
        xor bx,bx               ;reset interrupt counter
        mov cx,1000             ;get 1000 samples from soundblaster
        sti                     ;Enable interrupts
getsamples:
        call getsample
        loop getsamples
        cli
        push bx
        call reset
        pop bx
        cmp bx,975
        mov dx,sbslowerr
        ja error1
skip_sbtest:
        call reset
        pop dx                  ;retrieve start of file name
        mov ah,03c              ;create file
        mov cx,020              ;archive bit.  DX points to start of file name
        int 021
        jnc oof_ok
        mov dx,openerror
        mov ah,9
        int 021
        mov ax,04c01
        int 021

oof_ok: mov vochandle,ax
        mov bx,ax
        mov dx,vocheader
        mov cx,26
        mov ah,040
        int 021                 ;write voc header
        call install
        mov keypress,0
        mov escpress,0
wait_space:
        cmp keypress,0
        je wait_space
        mov keypress,0          ;reset flag
        cmp escpress,0
        if ne jmp exit
        test keydown,080        ;Was last code a break? (bit 7=1)
        jne wait_space          ;if it was, wait for a key to be pressed
wait_spacerelease:
        cmp keypress,0
        je wait_spacerelease
        mov keypress,0          ;reset flag
        cmp escpress,0
        if ne jmp exit
        test keydown,080        ;Was last code a break?
        je wait_spacerelease    ;if it was, wait for the key to be released
        cmp signaltest,0ff
        jne scan_again
        mov dx,startsample0
        mov ah,9
        int 021
        call getsample
        mov ah,al
wait_signal:
        cmp escpress,0
        if ne jmp exit
        call getsample
        xor al,ah
        jns wait_signal
        mov keypress,0          ;reset flag, maybe user pressed SPACE twice
scan_again:
        mov dx,startsample
        mov ah,9
        int 021
;
;Now start sampling.  Let the timer interrupt routine sample 8192 bytes
;in the 16K buffer, while the main routine compresses 8 samples in one
;byte in the actual, large buffer.  Then move the output pointer of the
;timer sampler down to the start again, process the remaining samples
;high in the 16K buffer, move down and start all over again.
;
        mov dx,baseadr
        add dl,0e               ;go to dsp_data_avail
        test dh
        if e mov dx,lptadr      ;holds port to IN from - see also timer routine
        mov di,outputbuf        ;es:di points to 16K sample buffer
        mov bx,outputbuf        ;process pointer
        mov cx,buffer           ;holds current paragraph of large buffer
        xor si,si               ;points in current paragraph
        in al,021
        and al,0fe              ;Enable timer interrupt!
        out 021,al              ;And GO!
firstpass:
        add bx,16               ;Point behind block of 8 samples to process
fp1:    cmp di,bx               ;Are 8 samples ready?
        jb fp1                  ;Wait until they are
        cmp di,outputbuf+8192+1024  ;The first pass code should be able to keep
                                ; up with the sampler.  So, when the output
                                ; pointer of the sampler gets way too high up
                                ; in the buffer this computer obviously is
        if a jmp scan_end       ; too slow.
        cmp b[bx-16],080        ;Is sample 0 a 1?
        jb sample0              ;Jump if not
        cmp b[bx-15],080
        jb sample1
        cmp b[bx-14],080
        jb sample2
        cmp b[bx-13],080
        jb sample3
        cmp b[bx-12],080
        jb sample4
        cmp b[bx-11],080
        jb sample5
        cmp b[bx-10],080
        jb sample6
        cmp b[bx-9],080
        jb sample7
        cmp b[bx-8],080
        jb sample8
        cmp b[bx-7],080
        jb sample9
        cmp b[bx-6],080
        jb sample10
        cmp b[bx-5],080
        jb sample11
        cmp b[bx-4],080
        jb sample12
        cmp b[bx-3],080
        jb sample13
        cmp b[bx-2],080
        jb sample14
        cmp b[bx-1],080      ;Is sample 15 a 1?
        jb sample15             ;Jump if it is not
        dec bx                  ;Point to final sample 1, to be included
        mov ah,0f               ;Store 0/15 and continue
        jmp storebyte
sample0:
        xor ah,ah               ;0 ones
        sub bx,16
        jmp short countzeroes
sample1:
        mov ah,01
        sub bx,15
        jmp short countzeroes
sample2:
        mov ah,02
        sub bx,14
        jmp short countzeroes
sample3:
        mov ah,03
        sub bx,13
        jmp short countzeroes
sample4:
        mov ah,04
        sub bx,12
        jmp short countzeroes
sample5:
        mov ah,05
        sub bx,11
        jmp short countzeroes
sample6:
        mov ah,06
        sub bx,10
        jmp short countzeroes
sample7:
        mov ah,07
        sub bx,9
        jmp short countzeroes
sample8:
        mov ah,08
        sub bx,8
        jmp short countzeroes
sample9:
        mov ah,09
        sub bx,7
        jmp short countzeroes
sample10:
        mov ah,0a
        sub bx,6
        jmp short countzeroes
sample11:
        mov ah,0b
        sub bx,5
        jmp short countzeroes
sample12:
        mov ah,0c
        sub bx,4
        jmp short countzeroes
sample13:
        mov ah,0d
        sub bx,3
        jmp short countzeroes
sample14:
        mov ah,0e
        sub bx,2
        jmp short countzeroes
sample15:
        mov ah,0f
        dec bx
countzeroes:
        add bx,16
fp2:    cmp di,bx               ;Are 8 samples ready?
        jb fp2                  ;Wait until they are
        cmp b[bx-16],080        ;Is sample 0 a 0?
        jnc zerosample0         ;Jump if not
        cmp b[bx-15],080
        jnc zerosample1
        cmp b[bx-14],080
        jnc zerosample2
        cmp b[bx-13],080
        jnc zerosample3
        cmp b[bx-12],080
        jnc zerosample4
        cmp b[bx-11],080
        jnc zerosample5
        cmp b[bx-10],080
        jnc zerosample6
        cmp b[bx-9],080
        jnc zerosample7
        cmp b[bx-8],080
        jnc zerosample8
        cmp b[bx-7],080
        jnc zerosample9
        cmp b[bx-6],080
        jnc zerosample10
        cmp b[bx-5],080
        jnc zerosample11
        cmp b[bx-4],080
        jnc zerosample12
        cmp b[bx-3],080
        jnc zerosample13
        cmp b[bx-2],080
        jnc zerosample14
        dec bx                  ;Point to final sample (0 or 1), to be included
        or ah,0f0               ;15 zeroes
        jmp short storebyte
zerosample0:
        sub bx,16
        jmp short storebyte
zerosample1:
        or ah,010
        sub bx,15
        jmp short storebyte
zerosample2:
        or ah,020
        sub bx,14
        jmp short storebyte
zerosample3:
        or ah,030
        sub bx,13
        jmp short storebyte
zerosample4:
        or ah,040
        sub bx,12
        jmp short storebyte
zerosample5:
        or ah,050
        sub bx,11
        jmp short storebyte
zerosample6:
        or ah,060
        sub bx,10
        jmp short storebyte
zerosample7:
        or ah,070
        sub bx,9
        jmp short storebyte
zerosample8:
        or ah,080
        sub bx,8
        jmp short storebyte
zerosample9:
        or ah,090
        sub bx,7
        jmp short storebyte
zerosample10:
        or ah,0a0
        sub bx,6
        jmp short storebyte
zerosample11:
        or ah,0b0
        sub bx,5
        jmp short storebyte
zerosample12:
        or ah,0c0
        sub bx,4
        jmp short storebyte
zerosample13:
        or ah,0d0
        sub bx,3
        jmp short storebyte
zerosample14:
        or ah,0e0
        sub bx,2
storebyte:
        mov ds,cx
        mov [si],ah             ;store the byte
        mov bp,cs
        mov ds,bp
        inc si
        cmp si,16
        if b jmp firstpass      ;If paragraph not full yet, continue
        cmp keypress,0          ;First check for a user interruption
        jne scan_end
        inc cx                  ;Next paragraph
        cmp cx,buftop
        jae scan_end            ;Exit if buffer full
        xor si,si
        cmp di,outputbuf+8192   ;Is there still enough memory left?
        if b jmp firstpass      ;If so then just continue
waitforbx:                      ;Make sure [BX] is up to date
        cmp di,bx
        jbe waitforbx
        mov bp,outputbuf        ;It's very convenient to have a non-interruptable
                                ; XCHG instruction here:
        xchg bp,di              ;Get pointer into BP and move DI down, simultaneously
secondpass0:
        xor ah,ah               ;Count things in AH
secondpass:
        cmp b[bx],080           ;It is a 1?
        jb thirdpass            ;Jump if not
        inc ah                  ;Include it
        cmp ah,010
        je thirdpass_dec        ;Jump if AH overflows
        inc bx
        cmp bx,bp
        jb secondpass           ;Jump back only if there are samples left
        mov bx,outputbuf        ;Start scanning from start of buffer again
        jmp storebyte           ;Store it, and continue into first pass
thirdpass_dec:
        dec ah                  ;Restore value of counter
thirdpass:
        cmp b[bx],080           ;Is it a 0?
        jnc thirdpass_end
        add ah,010
        jc thirdpass_end_dec
        inc bx
        cmp bx,bp
        jb thirdpass
        mov bx,outputbuf
        jmp storebyte
thirdpass_end_dec:
        sub ah,010              ;Restore AH
thirdpass_end:
        mov ds,cx
        mov [si],ah             ;Store byte
        push cs                 ;No free register available here
        pop ds
        inc si
        cmp si,16
        jb secondpass0          ;Little chance that the following code will
        inc cx                  ; ever be executed, but let's try to be complete.
        cmp cx,buftop
        jae scan_end
        xor si,si
        jmp secondpass0

scan_end:
        cli                     ;First stop that over-enthousiastic sampler
        push cx                 ;Current buffer paragraph
        push di                 ;Sample buffer pointer
        call reset              ;Resets DS too, enables normal interrupts
        pop di
        pop si
        cmp di,outputbuf+8192+1024      ;Was it a timing error?
        if a jmp too_slow
        cmp si,buftop           ;Out of memory?
        jb not_out_of_mem
        mov dx,memmsg
        mov ah,9
        int 021
not_out_of_mem:
        mov dx,writesample1
        mov ah,9
        int 021
        mov bx,xlatbuf
        mov ax,buffer
        dec ax
        mov es,ax
        xor cx,cx               ;Low counter of samples
        xor dx,dx               ;High counter (256*CX)
count_samples:
        mov ax,es
        inc ax
        mov es,ax
        cmp ax,si
        jae samples_counted
        mov di,15
        xor ah,ah
cs1:    mov al,es:[di]
        xlatb
        add cx,ax
        dec di
        jne cs1
        add dl,ch
        adc dh,0
        xor ch,ch
        jmp count_samples
samples_counted:
        mov ax,dx               ;# samples / 256
        call printlength
        mov dx,writesample2
        mov ah,9
        int 021
        mov ax,1000
        xor dx,dx
        div samplefreq
        add dx,dx
        cmp dx,samplefreq
        sbb ax,-1
        neg al                          ;al=256-(1 MHz / sample frequency)
        mov [offset datablock+4],al     ;store sample rate
        mov es,buffer
write_vocfile:
        mov bx,outputbuf
wv_para:
        xor di,di
wv_loop:
        mov al,es:[di]
        mov ah,al
        and al,0f
        je wv_loop1
wv_loop0:
        mov b[bx],010
        inc bx
        dec al
        jne wv_loop0
wv_loop1:
        sub ah,010
        jc wv_byteok
wv_loop11:
        mov b[bx],0f0
        inc bx
        sub ah,010
        jnc wv_loop11
wv_byteok:
        inc di
        cmp di,16
        jb wv_loop
        mov ax,es
        inc ax
        mov es,ax
        cmp ax,si
        jae wv_blockend
        cmp bx,outputbuf+16384-500      ;Space for 15*30 samples?
        jb wv_para
wv_blockend:
        push es
        sub bx,outputbuf-2
        mov [offset datablock+1],bx     ;store length
        mov bx,vochandle
        mov cx,6
        mov dx,datablock
        mov ah,040
        int 021                 ;save header of block
        mov cx,[offset datablock+1]
        sub cx,2
        mov dx,outputbuf
        mov ah,040
        int 021
        jc write_err
        cmp ax,cx
        jne write_err
        pop ax                  ;get back first paragraph to process
        mov es,ax               ;store
        cmp ax,si               ;still paragraphs left?
        jb write_vocfile
        mov dx,finishsample
        mov ah,9
        int 021
        cmp escpress,0
        jne exit
        call install            ;Resets ES
        mov keypress,0
        jmp scan_again

too_slow:
        mov dx,slowerror
        mov ah,9
        int 021
        jmp exit

write_err:
        mov dx,diskerror
        mov ah,9
        int 021
exit:   call reset
        mov bx,vochandle
        mov dx,terminator
        mov cx,1
        mov ah,040
        int 021                 ;write terminator
        mov ah,03e
        int 021                 ;close file
        mov ax,04c00
        int 021                 ;terminate



getnum: lodsb
        cmp al,020
        je getnum
        xor bx,bx
        xor ah,ah
getnum_loop:
        cmp al,'0'
        jb getnum_end
        cmp al,'9'
        ja getnum_end
        add bx,bx
        mov cx,bx
        add bx,bx
        add bx,bx
        add bx,cx
        sub al,'0'
        add bx,ax
        lodsb
        jmp getnum_loop
getnum_end:
        mov ax,bx
        dec si
        ret

install:
        xor ax,ax
        mov es,ax
        cli
        mov es:w[8*4],my_timinter
        cmp cs:baseadr,0
        if ne mov es:w[8*4],my_timinter3
        mov es:w[8*4+2],cs
        mov es:w[9*4],my_kbdinter
        mov es:w[9*4+2],cs
        push cs
        pop es                  ;reset ES
        in al,021
        mov original21,al
        or al,0fd
        out 021,al              ;disable everything except keyboard
        sti                     ;main program will enable timer interrupts
        mov al,043              ;counter 0, read LSB then MSB, mode 3
        out 043,al
        jmp $+2
        mov al,b[offset tunit]
        out 040,al              ;set counter, LSB first
        jmp $+2
        xor al,al
        out 040,al              ;MSB 0
        ret

reset:  cli
        xor ax,ax
        mov es,ax
        push cs
        pop ds
        mov si,timinter
        mov di,8*4
        mov cx,4
        cld
        rep movsw               ;reset keyboard and timer interrupts
        push cs
        pop es
        mov al,036              ;counter 0, r/w lsb then msb, mode 3, non-bcd
        out 043,al              ;reset mode of counter 0
        jmp $+2
        xor al,al
        out 040,al              ;Start counter again...!
        jmp $+2
        out 040,al
        jmp $+2
        mov al,original21
        out 021,al              ;enable interrupts
        sti                     ;and enable them here too
        ret

printlength:                    ;In: AX=# samples / 256
        xor dx,dx
        mov cx,samplefreq
        add cx,cx
        add cx,cx
        div cx                  ;(#samples/256)/(4*s.freq) = #smpls / (1024*s.f)
        mov cx,1000
        xor dh,dh
        call prdigit
        mov cx,100
        call prdigit
        mov cx,10
        call prdigit
        mov cx,1
        mov dh,1
prdigit:
        mov dl,'0'-1
prdigit1:
        inc dl
        sub ax,cx
        jnc prdigit1
        add ax,cx
        cmp dx,'0'
        je ret
        mov dh,dl
        push ax
        mov ah,2
        int 021
        pop ax
        ret


my_kbdinter:                    ;No STI here!  Otherwise AL is unstable...!
        push ax
        mov al,020
        out 020,al
        in al,060
        cmp al,0e0
        jz return_keyscan
        cmp al,0e1
        jz return_keyscan
        mov cs:keypress,0ff
        or al,cs:break
        cmp al,0f0
        jz breakexit
        mov cs:break,0
        mov cs:keydown,al               ;bit 7=1 if key released
        cmp al,1
        jne return_keyscan
        mov cs:escpress,0ff
        jmp return_keyscan
breakexit:
        mov cs:break,080
return_keyscan:
        in al,061
        push ax
        or al,080
        out 061,al
        pop ax
        out 061,al
        pop ax
        iret



my_timinter:            ;The heart of the sample routine
        mov al,020      ;Main routine shouldn't use AL!
        out 020,al      ;Send non-specific EOI to hardware
        in al,dx        ;Main routine should have DX pointing to LPT IN port
        stosb           ;Main routine should worry about buffer overflow
        iret            ;This also resets the interrupt flag


my_timinter2:
        mov al,020
        out 020,al
        iret            ;Used to test speed of computer


my_timinter3:           ;Used when reading from SoundBlaster
        mov al,020
        out 020,al
mti3_wait:
        in al,dx        ;dsp_read_status
        shl al,1
        jnc mti3_wait   ;shouldn't happen, but better be sure than sorry
        sub dl,4
        in al,dx        ;dsp_read_data
        cmp al,080-drempel
        sbb al,al       ;inverts the pixel, but that does not matter
        stosb           ;store byte
        add dl,2
mti3_wait_write:
        in al,dx        ;dsp_write_status
        shl al,1
        jc mti3_wait_write
        mov al,020      ;sample single byte
        out dx,al       ;dsp_write_data
        add dl,2        ;restore DX
        iret


my_timinter4:           ;Used to check SoundBlaster speed
        push ax
        mov al,020
        out 020,al
        pop ax
        inc bx
        iret


getsample:
        mov dx,baseadr
        test dx
        je getsample_lpt
        add dx,0e           ;dsp_data_avail
gs1:    in al,dx
        shl al,1
        jnc gs1
        sub dl,4            ;dsp_read_data
        in al,dx
        cmp al,080-drempel
        sbb al,al
        push ax
        add dl,2
gs2:    in al,dx            ;dsp_write_status
        shl al,1
        jc gs2
        mov al,020
        out dx,al           ;dsp_write_data
        pop ax
        ret
getsample_lpt:
        mov dx,lptadr
        in al,dx
        ret


intromsg:       db 0d,0a,"READVOC - Read VOC sample from LPT port or SoundBlaster - (c) 1994 G.A. Lunter",0d,0a
                db "          This program is public domain.",0d,0a,0d,0a,'$'
helpmsg:        db "Usage:  READVOC  [-b N]  [-s [adr]]  [-r NN]  [-t]  filename.VOC",0d,0a
                db 0d,0a
                db "-b N      specifies the LPT port to read from (default LPT1)",0d,0a
                db "-s [adr]  Use SoundBlaster, and specify base I/O address (default 0220)",0d,0a
                db "-r NN     specifies the sample frequency in kHz (default 20 kHz, min 5, max 40)",0d,0a
                db "-t        skips the signal test",0d,0a
                db 0d,0a
                db "Examples:  READVOC -b2 -r25 jetset.voc",0d,0a
                db "           READVOC -s 240 -r10 manic.voc",0d,0a
                db 0d,0a
                db "This program uses either the SoundBlaster A/D converter or the LPT tape inter-",0d,0a
                db "face of the Spectrum Emulator 'Z80' to record a 1-bit .VOC sample.  By default",0d,0a
                db "the tape interface is used; to select the SoundBlaster use the -s switch.",0d,0a
                db "For best results use a sample frequency at least 4 (not 2) times as high as the",0d,0a
                db "highest frequency in the signal, i.e. about 10 kHz for programs utilysing the",0d,0a
                db "standard tape format, higher for programs using turbo loaders.",0d,0a
                db "The VOC file can be compressed using e.g. PKZIP or ARJ to very small sizes.",0d,0a,0d,0a,'$'
startmsg:       db "Longest continuous sample size: about $"
startmsg2:      db " seconds.",0d,0a,0d,0a
                db "  þ Press SPACE to start sampling.",0d,0a
                db "  þ Press SPACE after each block to save it to disk.",0d,0a
                db "  þ Press ESC to save last block and exit.",0d,0a,0d,0a
                db "Do NOT press any other key while sampling!",0d,0a,0d,0a,'$'
slowerror:      db 0d,0a,0d,0a,"ERROR: Computer is too slow for this sample rate. Try using lower frequency.",0d,0a,'$'
diskerror:      db 0d,0a,0d,0a,"ERROR: Disk full while writing to VOC file.",0d,0a,'$'
openerror:      db "ERROR creating output file.",0d,0a,'$'
memerror:       db "ERROR: Not enough memory.",0d,0a,'$'
baseerr:        db "ERROR: Wrong SoundBlaster base address (must be between 0200 and 0280 incl)",0d,0a,'$'
blasterr:       db "ERROR: No SoundBlaster found!",0d,0a,'$'
sbslowerr:      db "ERROR: SoundBlaster can't handle this sample rate!",0d,0a,'$'
memmsg:         db 0d,0a,"Warning:  Memory full.  Sampling stopped.",0d,0a,'$'
startsample0:   db "Waiting for a signal... ",0d,0a,'$'
startsample:    db "Sampling... $"
writesample1:   db "($"
writesample2:   db " seconds), writing... $"
finishsample:   db "Finished.",0d,0a,'$'

xlatbuf         equ $+128               ;Allow 128 bytes for stack
outputbuf       equ xlatbuf+256         ;size of xlatbuf is 256 bytes

comsize_bytes   equ outputbuf+16384
comsize_para    equ (comsize_bytes+15)/16


